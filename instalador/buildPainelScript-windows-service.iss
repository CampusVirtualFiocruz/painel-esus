; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Painel eSUS - Windows Service"
#define MyAppVersion "v0.9.2.1-windows-service"
#define MyAppPublisher "Fiocruz"
#define MyAppURL "https://painelsaude.fiocruz.br"
#define MyAppExeName "painel-esus.exe"
#define MyAppAssocName MyAppName + ""
#define MyAppAssocExt ".myp"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt
#define ConfigExeName "config.exe"
#define rootPath "D:\a\painel-esus\painel-esus"
#define ServiceName "PaineleSUSService"

[Setup]
AppId={{91B895F7-3F8C-4B4C-A898-AF39516ADBFC}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\Fiocruz\{#MyAppName}
ChangesAssociations=yes
DisableProgramGroupPage=yes
OutputDir={#rootPath}\instalador\output
OutputBaseFilename=painel-esus-setup{#MyAppVersion}
SetupIconFile={#rootPath}\painel-esus\icon\Icon_Painel_Purple_ICO.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern
PrivilegesRequired=admin

[Languages]
Name: "brazilianportuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "installservice"; Description: "Executar como serviço"; GroupDescription: "Opções de serviço:"; Flags: unchecked

[Files]
Source: "{#rootPath}\painel-esus\dist\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#rootPath}\paineis-v2-front\static-files\*"; DestDir: "{app}\static-files"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#rootPath}\painel-esus\icon\*"; DestDir: "{app}\icon"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#rootPath}\painel-esus\dist\config.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#rootPath}\painel-esus\dist\painel-esus.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#rootPath}\painel-esus\ibge.csv"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#rootPath}\painel-esus\nssm.exe"; DestDir: "{app}"; Flags: ignoreversion

[Registry]
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#MyAppExeName},0"
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""
Root: HKA; Subkey: "Software\Classes\Applications\{#MyAppExeName}\SupportedTypes"; ValueType: string; ValueName: ".myp"; ValueData: ""

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{autoprograms}\Gerenciar Serviço\Iniciar Serviço"; Filename: "{app}\nssm.exe"; Parameters: "start {#ServiceName}"
Name: "{autoprograms}\Gerenciar Serviço\Parar Serviço"; Filename: "{app}\nssm.exe"; Parameters: "stop {#ServiceName}"
Name: "{autoprograms}\Gerenciar Serviço\Reiniciar Serviço"; Filename: "{app}\nssm.exe"; Parameters: "restart {#ServiceName}"

[Run]
; Instalação do serviço (caso opção estiver selecionada)
Filename: "{app}\nssm.exe"; Parameters: "install {#ServiceName} ""{app}\{#MyAppExeName}"""; \
    Flags: runhidden; StatusMsg: "Instalando serviço..."; Tasks: installservice
Filename: "{app}\nssm.exe"; Parameters: "set {#ServiceName} Description ""Serviço do Painel e-SUS APS"""; \
    Flags: runhidden; StatusMsg: "Configurando serviço..."; Tasks: installservice
Filename: "{app}\nssm.exe"; Parameters: "start {#ServiceName}"; \
    Flags: runhidden; StatusMsg: "Iniciando serviço..."; Tasks: installservice
Filename: "{app}\{#ConfigExeName}"; Description: "Abrir configuração após a instalação"; Flags: postinstall skipifsilent unchecked runascurrentuser; Check: ShouldRunExe

[UninstallRun]
; Remove o serviço durante a desinstalação
Filename: "{app}\nssm.exe"; Parameters: "stop {#ServiceName}"; Flags: runhidden
Filename: "{app}\nssm.exe"; Parameters: "remove {#ServiceName} confirm"; Flags: runhidden

[Code]
function ShouldRunExe(): Boolean;
begin
  Result := WizardSilent() = False;  // Apenas rodar se o instalador não estiver no modo silencioso
end;
